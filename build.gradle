// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
        classpath 'com.uphyca.gradle:gradle-android-apt-plugin:0.9.4'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
        classpath 'org.robolectric:robolectric-gradle-plugin:0.14.1'
    }
}

allprojects {
    repositories {
        jcenter()
        maven {
            url 'https://maven.fabric.io/public'
        }
    }

    project.plugins.whenPluginAdded { plugin ->
        if ("com.android.build.gradle.AppPlugin".equals(plugin.class.name)) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        } else if ("com.android.build.gradle.LibraryPlugin".equals(plugin.class.name)) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        }
    }
}

// http://www.gradle.org/docs/current/dsl/org.gradle.api.plugins.ExtraPropertiesExtension.html
project.ext {
    compileSdkVersion = 21
    buildToolsVersion = '21.1.1'

    // see http://tools.android.com/tech-docs/new-build-system/tips#TOC-Improving-Build-Server-performance.
    // CIサーバなどでは -PdisablePreDex をつける。 preDexが省略されてビルドが速くなる
    preDexLibs = !project.hasProperty('disablePreDex')

    supportPackageVersion = '21.0.2'
    jodaTimeVersion = '2.6'
    butterKnifeVersion = '6.0.0'

    junitVersion = '4.11'
    robolectricVersion = '2.4'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}
