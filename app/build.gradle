apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apply plugin: 'robolectric'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "jp.water_cell.android.template"
        minSdkVersion 15
        targetSdkVersion 18 // 19以上はRobolectricが未対応
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "com.google.android.apps.common.testing.testrunner.GoogleInstrumentationTestRunner"

        multiDexEnabled true
    }

    compileOptions {
        encoding = 'UTF-8'
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError true
        checkAllWarnings true
        warningsAsErrors true
        checkReleaseBuilds true
    }

    sourceSets {
        androidTest {
            setRoot('src/test')
        }
    }

    packagingOptions {
        // exclude duplicate files in apache commons
        // pickFirst なら最初に見つけたやつが apk に入る
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'LICENSE.txt'
    }

    signingConfigs {
        release
    }

    buildTypes {
        debug {
            versionNameSuffix '-debug'
            applicationIdSuffix '.debug'

            manifestPlaceholders = [google_maps_key: 'デバッグ用の証明書に対応するAPIキーをここに書く']
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            // gradle.properties にリリース署名用の設定があれば署名を有効化します。
            if (project.hasProperty('storeFile')) {
                signingConfig signingConfigs.release
            }

            manifestPlaceholders = [google_maps_key: 'リリース用の証明書に対応するAPIキーをここに書く']
        }
    }
}

if (project.hasProperty('storeFile')) {
    android.signingConfigs.release.storeFile = file(storeFile)
}
if (project.hasProperty('storePassword')) {
    android.signingConfigs.release.storePassword = storePassword
}
if (project.hasProperty('keyAlias')) {
    android.signingConfigs.release.keyAlias = keyAlias
}
if (project.hasProperty('keyPassword')) {
    android.signingConfigs.release.keyPassword = keyPassword
}

robolectric {
    // configure the set of classes for JUnit tests
    include '**/*Test.class'
    exclude '**/espresso/**/*.class'

    // configure max heap size of the test JVM
    maxHeapSize = '2048m'
}

tasks.withType(Test).whenTaskAdded {
    // play sevicesのVerifyError回避 (see https://github.com/robolectric/robolectric/issues/1186#issuecomment-53778180)
    it.jvmArgs '-noverify'
}

dependencies {
    compile "com.android.support:appcompat-v7:${project.supportPackageVersion}"

    compile "joda-time:joda-time:${project.jodaTimeVersion}"

    compile "com.jakewharton:butterknife:${project.butterKnifeVersion}"
    apt "com.jakewharton:butterknife:${project.butterKnifeVersion}"

    androidTestCompile("junit:junit:${project.junitVersion}") {
        exclude module: 'hamcrest-core'
    }
    androidTestCompile("org.robolectric:robolectric:${project.robolectricVersion}") {
        exclude module: 'classworlds'
        exclude module: 'commons-logging'
        exclude module: 'httpclient'
        exclude module: 'maven-artifact'
        exclude module: 'maven-artifact-manager'
        exclude module: 'maven-error-diagnostics'
        exclude module: 'maven-model'
        exclude module: 'maven-project'
        exclude module: 'maven-settings'
        exclude module: 'plexus-container-default'
        exclude module: 'plexus-interpolation'
        exclude module: 'plexus-utils'
        exclude module: 'wagon-file'
        exclude module: 'wagon-http-lightweight'
        exclude module: 'wagon-provider-api'
    }
    androidTestCompile files('libs/espresso-1.1-bundled.jar')
}

// テスト対象のActivityがFragmentActivityを継承しているとNoClassDefFoundError出る問題
// (see https://groups.google.com/d/msg/adt-dev/fzPf8aVZmNg/uaoYz2WMNjwJ)
configurations {
    androidTestCompile.exclude module: 'support-v4'
}
